' ########################################################################################
' Microsoft Windows
' File: CW_UpDown_01.bas
' Contents: CWindow - UpDown control
' Compiler: FreeBasic 32 & 64 bit
' Copyright (c) 2025 José Roca. Freeware. Use at your own risk.
' THIS CODE AND INFORMATION IS PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND, EITHER
' EXPRESSED OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE IMPLIED WARRANTIES OF
' MERCHANTABILITY AND/OR FITNESS FOR A PARTICULAR PURPOSE.
' ########################################################################################

#define UNICODE
#INCLUDE ONCE "AfxNova/CWindow.inc"
USING AfxNova

DECLARE FUNCTION wWinMain (BYVAL hInstance AS HINSTANCE, _
                           BYVAL hPrevInstance AS HINSTANCE, _
                           BYVAL pwszCmdLine AS WSTRING PTR, _
                           BYVAL nCmdShow AS LONG) AS LONG
   END wWinMain(GetModuleHandleW(NULL), NULL, wCommand(), SW_NORMAL)

' // Forward declaration
DECLARE FUNCTION WndProc (BYVAL hwnd AS HWND, BYVAL uMsg AS UINT, BYVAL wParam AS WPARAM, BYVAL lParam AS LPARAM) AS LRESULT

CONST IDC_UPDOWN = 1001
CONST IDC_EDIT = 1002

' ========================================================================================
' Main
' ========================================================================================
FUNCTION wWinMain (BYVAL hInstance AS HINSTANCE, _
                   BYVAL hPrevInstance AS HINSTANCE, _
                   BYVAL pwszCmdLine AS WSTRING PTR, _
                   BYVAL nCmdShow AS LONG) AS LONG

   ' // Set process DPI aware
   SetProcessDpiAwareness(PROCESS_SYSTEM_DPI_AWARE)
   ' // Enable visual styles without including a manifest file
   AfxEnableVisualStyles

   ' // Creates the main window
   DIM pWindow AS CWindow = "MyClassName"   ' Use the name you wish
   DIM hWin AS HWND = pWindow.Create(NULL, "CWindow - UpDown control", @WndProc)
   ' // Sizes it by setting the wanted width and height of its client area
   pWindow.SetClientSize(400, 220)
   ' // Centers the window
   pWindow.Center

   ' // Adds a label control
   pWindow.AddControl("Label", hWin, -1, "How many lines?", 80, 55, 100, 23)
   ' // Adds an edit control
   DIM hEdit AS HWND = pWindow.AddControl("Edit", hWin, IDC_EDIT, "", 200, 55, 50, 23)
   ' // Add an UpDown control (the size and position will be automatically adjusted to the buddy control)
   DIM hUpDown AS HWND = pWindow.AddControl("UpDown", hWin, IDC_UPDOWN)
   ' // Set the edit control as buddy of the updown control
   UpDown_SetBuddy(hUpDown, hEdit)
   ' // Set the base
   UpDown_SetBase(hUpDown, 10)
   ' // Set the range
   UpDown_SetRange32(hUpDown, 0, 100)
   ' // Set the default initial value
   UpDown_SetPos32(hUpDown, 1)
   ' // Set the focus
   SetFocus hEdit

   ' // Adds a button
   pWindow.AddControl("Button", hWin, IDCANCEL, "&Close", 270, 155, 75, 30)
   ' // Anchors the button to the bottom and the right side of the main window
   pWindow.AnchorControl(IDCANCEL, AFX_ANCHOR_BOTTOM_RIGHT)

   ' // Displays the window and dispatches the Windows messages
   FUNCTION = pWindow.DoEvents(nCmdShow)

END FUNCTION
' ========================================================================================

' ========================================================================================
' Main window procedure
' ========================================================================================
FUNCTION WndProc (BYVAL hwnd AS HWND, BYVAL uMsg AS UINT, BYVAL wParam AS WPARAM, BYVAL lParam AS LPARAM) AS LRESULT

   SELECT CASE uMsg

      ' // If an application processes this message, it should return zero to continue
      ' // creation of the window. If the application returns –1, the window is destroyed
      ' // and the CreateWindowExW function returns a NULL handle.
      CASE WM_CREATE
         RETURN 0

      ' // Sent when the user selects a command item from a menu, when a control sends a
      ' // notification message to its parent window, or when an accelerator keystroke is translated.
      CASE WM_COMMAND
         SELECT CASE CBCTL(wParam, lParam)
            CASE IDCANCEL
               ' // If ESC key pressed, close the application by sending an WM_CLOSE message
               IF CBCTLMSG(wParam, lParam) = BN_CLICKED THEN SendMessageW(hwnd, WM_CLOSE, 0, 0)
         END SELECT
         RETURN 0

'      CASE WM_NOTIFY
'         ' // Process notification messages
'         DIM ptnmhdr AS NMHDR PTR = CAST(NMHDR PTR, lParam)
'         SELECT CASE ptnmhdr->idFrom
'            CASE IDC_UPDOWN
'               IF ptnmhdr->code = UDN_DELTAPOS THEN
'                  ' Put your code here, e.g.
'                  DIM ptnmud AS NMUPDOWN PTR = CAST(NMUPDOWN PTR, lParam)
'                  'ptnmud->iPos : Up-down control's current position.
'print ptnmud->iPos; "---"
'                  ' ptnmud->iDelta : proposed change in the up-down control's position.
'                  ' Return nonzero to prevent the change in the control's position, or zero to allow the change.
'               END IF
'               EXIT FUNCTION
'         END SELECT

      CASE WM_NOTIFY
         ' // Process notification messages
         DIM updown AS NMUPDOWN
         CBNMTYPESET(updown, wParam, lParam)
         IF updown.hdr.idFrom = IDC_UPDOWN AND updown.hdr.code = UDN_DELTAPOS THEN
            ' updown.iPos : Up-down control's current position.
            ' updown.iDelta : proposed change in the up-down control's position.
            ' Return nonzero to prevent the change in the control's position, or zero to allow the change.
         END IF

      CASE WM_DESTROY
         ' // End the application by sending an WM_QUIT message
         PostQuitMessage(0)
         RETURN 0

   END SELECT

   ' // Default processing of Windows messages
   FUNCTION = DefWindowProcW(hwnd, uMsg, wParam, lParam)

END FUNCTION
' ========================================================================================
